import "./markdown.gdn"

// A list of all the files in `website_dir` that are templates.
//
// Returns absolute paths.
fun template_files(website_dir: Path): List<Path> {
  // TODO: a ? syntax.
  let dir_files = list_directory(website_dir).or_error()
  dir_files.filter(fun(p: Path) { p.p.ends_with(".tmpl.html") })
}

fun markdown_files(website_dir: Path): List<Path> {
  let dir_files = list_directory(website_dir).or_error()
  dir_files.filter(fun(p: Path) { p.extension() == Some("md") })
}

// Convert "foo.tmpl.html" to "foo.html".
fun output_path(template_path: String): Path {
  Path{ p: template_path.strip_suffix(".tmpl.html") ^ ".html"}
}

fun write_type_page(type_name: String, website_dir: Path, base_tmpl_path: Path): Unit {
  match type_doc_comment(type_name) {
    Some(doc_comment) => {
      let dest_path = Path{ p: "".join([website_dir.p, "/type:", type_name, ".html"]) }

      let src = match type_source(type_name) {
        Some(src) => {
          "```\n" ^ (src ^ "\n```")
        }
        None => ""
      }
      let md_src = "".join(["# `", type_name, "` type\n\n" , src, "\n\n", doc_comment])
      let page_src = render_markdown(md_src)
      write_website_page(base_tmpl_path, page_src, dest_path)
    }
    None => {}
  }
}

fun build_site(): Unit {
  let website_dir = source_dir_path().parent().parent().join("website")
  let tmpl_paths = template_files(website_dir)

  let base_tmpl_path = website_dir.join("template.html")

  for tmpl_path in tmpl_paths {
    let dest_path = output_path(tmpl_path.p)
    write_website_file(base_tmpl_path, tmpl_path, dest_path)
  }

  for md_path in markdown_files(website_dir) {
    let dest_path = Path{ p: md_path.p.strip_suffix(".md") ^ ".html" }
    let page_src_md = md_path.read().or_error()
    let page_src = render_markdown(substitute_shorthand(page_src_md))
    write_website_page(base_tmpl_path, page_src, dest_path)
  }

  for type_name in built_in_types() {
    write_type_page(type_name, website_dir, base_tmpl_path)
  }
}

fun join_with_commas_and(items: List<String>): String {
  let last_item = match items.last() {
    Some(item) => item,
    None => return "",
  }

  let but_last = items.slice(0, -1)
  if but_last.is_empty() {
    return last_item
  }

  " and ".join([", ".join(but_last), last_item ^ "."])
}

// Replace all __FOO strings in markdown `src` with runtime values.
fun substitute_shorthand(src: String): String {
  // TODO: this is duplicated with markdown.gdn. This should probably
  // be a built-in constant.
  let KEYWORDS = [
    "let", "fun", "enum", "struct", "external", "internal", "import", "if", "else", "while", "return", "test",
    "match", "break", "continue", "for", "in", "assert",
  ]

  let keyword_links = KEYWORDS.map(fun(keyword: String) {
    "".join(["[`", keyword, "`](./keyword:", keyword, ".html)"])
  })

  let keyword_links = join_with_commas_and(keyword_links)

  let type_links = built_in_types().map(fun(name: String) {
    "".join(["[`", name, "`](./type:", name, ".html)"])
  })

  let type_links = join_with_commas_and(type_links)

  src.replace("__KEYWORDS", keyword_links).replace("__BUILTIN_TYPES", type_links)
}

fun write_website_file(base_tmpl_path: Path, page_path: Path, dest_path: Path): Unit {
  let page_src = page_path.read().or_error()
  write_website_page(base_tmpl_path, page_src, dest_path)
}

fun write_website_page(base_tmpl_path: Path, page_src: String, dest_path: Path): Unit {
  let base_tmpl_src = base_tmpl_path.read().or_error()
  let (header, footer) = match base_tmpl_src.split_once("MAIN_CONTENT") {
    Some(s) => s,
    None => ("", "")
  }

  let src = "".join([header.trim_right(), page_src, footer.trim_left()])
  write_file(src, dest_path).or_error()
}

{
  build_site()
}

fun main(_: List<String>) {
  build_site()
}

fun source_dir_path(): Path {
  source_directory().or_exception()
}

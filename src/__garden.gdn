// Only used in tests.
import "__fs.gdn" as fs

// Check the syntax of `src`.
external fun check_snippet(src: String): Result<Unit, List<String>> {
  __BUILTIN_IMPLEMENTATION
}

test check_snippet_good {
  match check_snippet("fun foo() {}") {
    Ok(_) => {}
    Err(_) => {
      assert(False)
    }
  }
}

test check_snippet_bad_syntax {
  match check_snippet("fun foo(") {
    Ok(_) => {
      assert(False)
    }
    Err(_) => {}
  }
}

test check_snippet_bad_name {
  match check_snippet("fun foo() { bar() }") {
    Ok(_) => {
      assert(False)
    }
    Err(_) => {}
  }
}

// Split `src` into individual lexed tokens, along with their offsets
// in the original string.
external fun lex(src: String): List<(Int, String)> {
  __BUILTIN_IMPLEMENTATION
}

test lex {
  // Basic case.
  assert(lex("a 123 \"b c d\"") == [(0, "a"), (2, "123"), (6, "\"b c d\"")])

  // We should include preceding comments on tokens.
  assert(lex("a // b\n c") == [(0, "a"), (2, "// b\n"), (8, "c")])

  // We should include comments that have no token afterwards.
  assert(lex("a // b\n") == [(0, "a"), (2, "// b\n")])

  // Comments should not require a newline at EOF.
  assert(lex("a // b") == [(0, "a"), (2, "// b")])
}

// Get the doc comment for `fun_name` in the namespace `ns`.
external fun doc_comment(ns: Namespace, fun_name: String): Option<String> {
  __BUILTIN_IMPLEMENTATION
}

test doc_comment {
  match doc_comment(fs, "write_file") {
    Some(_) => {}
    None => { assert(False) }
  }
}

// Get the doc comment for type `type_name`.
external fun doc_comment_for_type(type_name: String): Option<String> {
  __BUILTIN_IMPLEMENTATION
}

test doc_comment_for_type {
  match doc_comment_for_type("Unit") {
    Some(_) => {}
    None => { assert(False) }
  }

  match doc_comment_for_type("NoSuchTypeExists") {
    Some(_) => { assert(False) }
    None => {}
  }
}

// A list of all the type names in the prelude, `String`, `Option` and
// so on. The list is sorted alphabetically.
external fun prelude_types(): List<String> {
  __BUILTIN_IMPLEMENTATION
}

test prelude_types {
  assert(prelude_types().contains("Unit"))
  assert(prelude_types().contains("Fun"))
  assert(prelude_types().contains("String"))

  assert(prelude_types().first() == Some("Bool"))
}

// All the reserved words in Garden. These are keywords that cannot be
// used as variable names.
external fun keywords(): List<String> {
  // TODO: Add support for constants so this doesn't need to be a function.
  [
    "let", "fun", "enum", "struct", "external", "internal",
    "import", "if", "else", "while", "return", "test",
    "match", "break", "continue", "for", "in", "assert",
    "as", "method",
  ]
}

// Get the source code for the type definition of `name`.
external fun source_for_type(name: String): Option<String> {
  __BUILTIN_IMPLEMENTATION
}

test source_for_type {
  match source_for_type("Unit") {
    Some(source) => {
      assert(source.starts_with("// "))
    }
    None => {
      assert(False)
    }
  }
}

// Get the source code for the function definition of `name`.
external fun source_for_fun(name: String): Option<String> {
  __BUILTIN_IMPLEMENTATION
}

test source_for_fun {
  // Built-in function.
  match source_for_fun("source_for_fun") {
    Some(source) => {
      assert(source.starts_with("// "))
    }
    None => {
      assert(False)
    }
  }

  // Function implemented in Garden.
  match source_for_fun("todo") {
    Some(source) => {
      assert(source.starts_with("// "))
    }
    None => {
      assert(False)
    }
  }
}

// All the `external` functions in `ns`.
external fun namespace_functions(ns: Namespace): List<String> {
  // This is deliberately not a method on Namespace, so code
  // completion isn't confusing.
  __BUILTIN_IMPLEMENTATION
}

test namespace_functions {
  let items = namespace_functions(fs)
  assert(items.contains("list_directory"))
}
